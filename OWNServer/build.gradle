apply plugin:'application'
mainClassName = "org.programmatori.iot.own.server.OWNServer"
applicationDefaultJvmArgs = ["-Dlogback.configurationFile=conf/logback.xml"]
// -Dlogging.config="/Users/moreno/Programmazione/git/my/OWN/OWNServer/conf/logback.xml"

version = '0.5.0'
jar {
	manifest {
		attributes 'Implementation-Title': project.name,
		'Implementation-Version': project.version,
		'Built-By': System.getProperty('user.name'),
		'Built-JDK': System.getProperty('java.version'),
		'Built-Host': InetAddress.getLocalHost().getHostName(),
		'Built-Date': new Date(),
		'Source-Compatibility': project.sourceCompatibility,
		'Target-Compatibility': project.targetCompatibility
	}
}

repositories {
	maven { url "http://clojars.org/repo/" }
}

dependencies {
	compile project(':OWN-SDK')
	//compile group: 'commons-collections', name: 'commons-collections', version: '3.+'

	runtime project(':OWNEngine-Emulator')
	runtime project(':OWNEngine-L4686Sdk')
	//runtime project(':OWNEngine-SCSGate')
	runtime project(':OWNPlugin-Map')
	runtime project(':OWNPlugin-Power')
	runtime project(':OWNPlugin-Remote')
	runtime project(':OWNPlugin-System')
	//runtime project(':OWNPlugin-HomeKit')

    testCompile 'com.github.openwebnet:rx-openwebnet:2.1.1'
}

startScripts {
	classpath += files('src/dist/conf')
	doLast {
		def windowsScriptFile = file getWindowsScript()
		def unixScriptFile    = file getUnixScript()
		windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\conf', '%APP_HOME%\\conf')
		unixScriptFile.text  = unixScriptFile.text.replace('$APP_HOME/lib/conf', '$APP_HOME/conf')
	}
}
