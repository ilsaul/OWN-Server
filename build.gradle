buildscript {
	repositories {
		jcenter()
	}

	dependencies {
		classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.+'
	}
}

plugins {
	id "org.sonarqube" version "2.0.1"
}

/**
 * Get the name of the working branch of the project
 *
 * @return Name of the working branch
 */
def getWorkingBranch() {
	// Triple double-quotes for the breaklines
	def workingBranch = """git --git-dir=${rootDir}/.git
							--work-tree=${rootDir}
							rev-parse --abbrev-ref HEAD""".execute().text.trim()
	logger.debug("Working branch: " + workingBranch)
	return workingBranch
}

version = '0.4.7-Develop'
sonarqube {
//	branch = "develop"
	properties {
		property "sonar.branch", getWorkingBranch()
		property "sonar.links.issue", "http://moreno.ga/bugs"
		property "sonar.links.homepage", "http://moreno.ga"
	}
}

task (branch) << {
	println "Actual branch: " + getWorkingBranch()
}
apply plugin: 'com.github.ben-manes.versions'

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'jacoco'
	test.finalizedBy jacocoTestReport
	jacoco {
		toolVersion = '0.7.7.201606060606'
	}

	repositories {
		mavenLocal()
		jcenter()
		mavenCentral()
	}

	dependencies {
		// Log of each module
		compile 'org.slf4j:slf4j-api:1.+'
		runtime 'ch.qos.logback:logback-classic:1.+'

		// Test
		testCompile group: 'junit', name: 'junit', version: '4.+'
	}

	sourceCompatibility = 1.6
}